plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
}

android {
    namespace 'one.tesseract.devwallet'

    compileSdk rootProject.ext.compileSdkVersion
    ndkVersion rootProject.ext.ndkVersion

    defaultConfig {
        applicationId "one.tesseract.devwallet"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

task fixNotCopyingRustJniLibs(type: Delete) {
    delete layout.buildDirectory.dir("intermediates/merged_jni_libs")
}

project.afterEvaluate {
    tasks.getByName("mergeDebugJniLibFolders").dependsOn("cargoBuild")
    tasks.getByName("mergeReleaseJniLibFolders").dependsOn("cargoBuild")
    tasks.getByName("cargoBuild").dependsOn("fixNotCopyingRustJniLibs")
}

cargo {
    module  = "../common"       // Or whatever directory contains your Cargo.toml
    libname = "wallet"          // Or whatever matches Cargo.toml's [package] name.
    targets = rootProject.ext.rustNdkTargets  // See bellow for a longer list of options
    targetDirectory = '../target'
    rustupChannel = "nightly"
    features {
        all()
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'

    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.ext.navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.ext.navVersion"

    implementation 'one.tesseract.android:crabdroid'
    implementation 'one.tesseract.android:tesseract-ipc'
    implementation 'one.tesseract.android:tesseract-ipc-service'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}