// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    def ndkVersionProp = properties.getProperty('ndk.version')
    def targetsString = properties.getProperty('ndk.targets')
    def targetsProp = targetsString == null ? null : new ArrayList<>(Arrays.asList(targetsString.split(",")))

    def DEFAULT_NDK_VERSION = '25.1.8937393'
    def DEFAULT_NDK_TARGETS = ["arm", "x86", "arm64", "x86_64"]

    ext {
        compileSdkVersion = 32
        minSdkVersion = 24
        targetSdkVersion = 32
        ndkVersion = ndkVersionProp == null ? DEFAULT_NDK_VERSION : ndkVersionProp
        rustNdkTargets = targetsProp == null ? DEFAULT_NDK_TARGETS : targetsProp
    }
}

plugins {
    id 'com.android.application' version '7.3.1' apply false
    id 'com.android.library' version '7.3.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.20' apply false
    id "org.mozilla.rust-android-gradle.rust-android" version "0.9.3" apply false
}